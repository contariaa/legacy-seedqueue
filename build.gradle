plugins {
    id "fabric-loom" version "1.11-SNAPSHOT"
    id "legacy-looming" version "1.10-SNAPSHOT"
    id "maven-publish"
}

version = "$mod_version+$target_version"
group = maven_group

base {
    archivesName = archives_base_name
}

repositories {
    maven { url "https://jitpack.io" }
}

loom {
    decompilers {
        vineflower {
            options.putAll(["mark-corresponding-synthetics": "1", "indent-string": "    "])
        }
    }
    mixin {
        useLegacyMixinAp = false
    }
    runs.client.programArgs "--userProperties", "{}"
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings(legacy.yarn(project.minecraft_version, project.yarn_mappings))
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
    filesMatching "fabric.mod.json", {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(8)
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

jar {
    from "LICENSE"
}

publishing {
    publications.create("mavenJava", MavenPublication) {
        from components.java
    }
    repositories {}
}
